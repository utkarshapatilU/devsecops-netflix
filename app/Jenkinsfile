pipeline {
    agent any

    environment {
        REGISTRY      = "docker.io"
        DOCKERHUB_ID  = "saiee2002"
        IMAGE_NAME    = "netflix"
        K8S_NAMESPACE = "netflix"
        SONARQUBE_ENV = "sonarqube"
    }

    options {
        buildDiscarder(logRotator(numToKeepStr: '10'))
        timestamps()
    }

    stages {

        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Install & Test') {
            steps {
                dir('app') {
                    sh 'npm ci || npm install'
                    sh 'npm test || echo "Tests failed or missing"'
                }
            }
        }

        stage('SonarQube Analysis') {
            steps {
                withSonarQubeEnv("${SONARQUBE_ENV}") {
                    sh """
                      sonar-scanner \
                        -Dsonar.projectKey=netflix-clone \
                        -Dsonar.sources=app/src
                    """
                }
            }
        }

        stage('Quality Gate') {
            steps {
                timeout(time: 3, unit: 'MINUTES') {
                    script {
                        def qg = waitForQualityGate()
                        if (qg.status != 'OK') {
                            error "Pipeline aborted due to quality gate failure: ${qg.status}"
                        }
                    }
                }
            }
        }

        stage('Build Docker Image') {
            steps {
                script {
                    env.IMAGE_TAG = "${env.BUILD_NUMBER}"
                    sh "docker build -t ${DOCKERHUB_ID}/${IMAGE_NAME}:${IMAGE_TAG} ."
                }
            }
        }

        stage('Image Security Scan (Trivy)') {
            steps {
                sh """
                  trivy image --exit-code 1 --severity CRITICAL,HIGH \
                    ${DOCKERHUB_ID}/${IMAGE_NAME}:${IMAGE_TAG}
                """
            }
        }

        stage('Push to DockerHub') {
            steps {
                withCredentials([usernamePassword(
                    credentialsId: 'dockerhub-creds',
                    usernameVariable: 'DOCKER_USER',
                    passwordVariable: 'DOCKER_PASS'
                )]) {
                    sh """
                      echo "$DOCKER_PASS" | docker login -u "$DOCKER_USER" --password-stdin
                      docker push ${DOCKERHUB_ID}/${IMAGE_NAME}:${IMAGE_TAG}
                      docker logout
                    """
                }
            }
        }

        stage('Update Helm values (GitOps)') {
            steps {
                script {
                    sh """
                      sed -i 's/tag: .*/tag: "${IMAGE_TAG}"/' app/helm/netflix/values.yaml
                      git config user.email "jenkins@local"
                      git config user.name "Jenkins"
                      git add app/helm/netflix/values.yaml
                      git commit -m "Update image tag to ${IMAGE_TAG}" || echo "No changes"
                    """
                    withCredentials([usernamePassword(
                        credentialsId: 'github-creds',
                        usernameVariable: 'GIT_USER',
                        passwordVariable: 'GIT_PAT'
                    )]) {
                        sh """
                          git push https://$GIT_USER:$GIT_PAT@github.com/utkarshapatilU/devsecops-netflix.git HEAD:main
                        """
                    }
                }
            }
        }
    }

    post {
        success {
            echo "Build SUCCESS: ${DOCKERHUB_ID}/${IMAGE_NAME}:${IMAGE_TAG}"
        }
        failure {
            echo "Build FAILED. Check Jenkins logs."
        }
    }
}
